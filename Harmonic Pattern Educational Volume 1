// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © RozaniGhani-RG
// Credits to Scott M Carney, author of Harmonic Trading: Volume One
// Bullish Ideal Bat         - Page 72
// Bearish Ideal Bat         - Page 83
// Bullish Perfect Bat       - Page 91
// Bearish Perfect Bat       - Page 94
// Bullish Ideal Gartley     - Page 99
// Bearish Ideal Gartley     - Page 106
// Bullish Perfect Gartley   - Page 115
// Bearish Perfect Gartley   - Page 118
// Bullish Ideal Crab        - Page 123
// Bearish Ideal Crab        - Page 130
// Bullish Perfect Crab      - Page 143
// Bearish Perfect Crab      - Page 146
// Bullish Ideal Deep Crab   - Page 137
// Bearish Ideal Deep Crab   - Page 140
// Bullish Ideal Butterfly   - Page 150
// Bearish Ideal Butterfly   - Page 158
// Bullish Perfect Butterfly - Page 163
// Bearish Perfect Butterfly - Page 166

//@version=4
study("Harmonic Pattern Educational Volume 1 (Source Code)", shorttitle = "HPEV1_SC", precision = 4)

// 0. Input
// 1. array
// 2. Ternary conditional operator and variables
// 3. Custom functions
// 4. array and custom functions for table
// 5. Compile custom functions
// 6. Construct
// 7. if statement

// 0. Input
G0              = " Direction       Type            Pattern"
G1              = "Font Size"
T0              = "Small font size recommended for mobile app or multiple layout"
input_direction = input("Bullish", "", options = ["Bullish", "Bearish"],                               inline = "0", group = G0)
input_type      = input("Ideal",   "", options = ["Ideal",   "Perfect"],                               inline = "0", group = G0)
input_pattern   = input("Bat",     "", options = ["Bat", "Gartley", "Crab", "Deep Crab", "Butterfly"], inline = "0", group = G0)
input_font      = input("Normal",  "", options = ["Normal", "Small"],                                  group = G1, tooltip = T0)

// 1. array
arr_color             = array.new_color(),  arr_color := array.from(color.black, color.teal, color.red)
//  array float ideal                                                           X      A      B      C      D
arr_float_i_Bat       = array.new_float(),  arr_float_i_Bat       := array.from(1.000, 0.000, 0.5,   0.236, 0.886)
arr_float_i_Gartley   = array.new_float(),  arr_float_i_Gartley   := array.from(1.000, 0.000, 0.618, 0.236, 0.786)
arr_float_i_Crab      = array.new_float(),  arr_float_i_Crab      := array.from(1.000, 0.000, 0.618, 0.236, 1.618)
arr_float_i_DeepCrab  = array.new_float(),  arr_float_i_DeepCrab  := array.from(1.000, 0.000, 0.886, 0.236, 1.618)
arr_float_i_Butterfly = array.new_float(),  arr_float_i_Butterfly := array.from(1.000, 0.000, 0.786, 0.236, 1.27)
//  array float ideal                                                           X      A      B      C      D
arr_float_p_Bat       = array.new_float(),  arr_float_p_Bat       := array.from(1.000, 0.000, 0.5,   0.236, 0.886)
arr_float_p_Gartley   = array.new_float(),  arr_float_p_Gartley   := array.from(1.000, 0.000, 0.618, 0.236, 0.786)
arr_float_p_Crab      = array.new_float(),  arr_float_p_Crab      := array.from(1.000, 0.000, 0.618, 0.236, 1.618)
arr_float_p_Butterfly = array.new_float(),  arr_float_p_Butterfly := array.from(1.000, 0.000, 0.786, 0.236, 1.27)
//  array range ideal                                                           XB               AC               BD               XD
arr_label_i_Bat       = array.new_string(), arr_label_i_Bat       := array.from("0.382;\n0.5",   "0.382;\n0.886", "1.618;\n2.618", "0.886")
arr_label_i_Gartley   = array.new_string(), arr_label_i_Gartley   := array.from("0.618",         "0.382;\n0.886", "1.13;\n1.618",  "0.786")
arr_label_i_Crab      = array.new_string(), arr_label_i_Crab      := array.from("0.382;\n0.618", "0.382;\n0.886", "2.618;\n3.618", "1.618")
arr_label_i_DeepCrab  = array.new_string(), arr_label_i_DeepCrab  := array.from("0.886",         "0.382;\n0.886", "2.24;\n3.618",  "1.618")
arr_label_i_Butterfly = array.new_string(), arr_label_i_Butterfly := array.from("0.786",         "0.382;\n0.886", "1.618;\n2.24",  "1.27")
//  array range perfect                                                         XB               AC               BD               XD
arr_label_p_Bat       = array.new_string(), arr_label_p_Bat       := array.from("0.5",           "0.5;\n0.886",   "2.0",           "0.886")
arr_label_p_Gartley   = array.new_string(), arr_label_p_Gartley   := array.from("0.618",         "0.618",         "1.618",         "0.786")
arr_label_p_Crab      = array.new_string(), arr_label_p_Crab      := array.from("0.618",         "0.5;\n0.886",   "3.14",          "1.618")
arr_label_p_Butterfly = array.new_string(), arr_label_p_Butterfly := array.from("0.786",         "0.5;\n0.886",   "1.618",         "1.27")
//  array table                                                                 XB        AC               BD              XD
arr_title_table       = array.new_string(), arr_title_table       := array.from("B = XA", "C = AB",        "D = BC",       "D = XA")
//  array label ideal                                                           XB        AC               BD              XD
arr_dir_i_Bat         = array.new_float(),  arr_dir_i_Bat         := array.from(0.750,    0.118,           0.693,          0.943)
arr_dir_i_Gartley     = array.new_float(),  arr_dir_i_Gartley     := array.from(0.809,    0.118,           0.702,          0.893)
arr_dir_i_Crab        = array.new_float(),  arr_dir_i_Crab        := array.from(0.809,    0.118,           1.118,          1.309)
arr_dir_i_DeepCrab    = array.new_float(),  arr_dir_i_DeepCrab    := array.from(0.943,    0.118,           1.252,          1.309)
arr_dir_i_Butterfly   = array.new_float(),  arr_dir_i_Butterfly   := array.from(0.893,    0.118,           1.028,          1.135)
//  array label perfect                                                         XB        AC               BD              XD
arr_dir_p_Bat         = array.new_float(),  arr_dir_p_Bat         := array.from(0.750,    0.118,           0.693,          0.943)
arr_dir_p_Gartley     = array.new_float(),  arr_dir_p_Gartley     := array.from(0.809,    0.118,           0.702,          0.893)
arr_dir_p_Crab        = array.new_float(),  arr_dir_p_Crab        := array.from(0.809,    0.118,           1.118,          1.309)
arr_dir_p_Butterfly   = array.new_float(),  arr_dir_p_Butterfly   := array.from(0.893,    0.118,           1.028,          1.135)
// 0 - Bat, 1 - Gartley, 1 - Crab, 2 - Deep Crab, 3 - Butterfly
// Reference                                                                     0     1      2      3      4
arr_ideal_bull        = array.new_string(), arr_ideal_bull         := array.from("72", "99",  "123", "137", "150")
arr_ideal_bear        = array.new_string(), arr_ideal_bear         := array.from("83", "106", "130", "140", "158")
// 0 - Bat, 1 - Gartley, 1 - Crab, 2 - Butterfly
// Reference                                                                     0      1      2     3
arr_perfect_bull      = array.new_string(), arr_perfect_bull       := array.from("91", "115", "143", "163")
arr_perfect_bear      = array.new_string(), arr_perfect_bear       := array.from("94", "118", "146", "164")

// 2. Ternary conditional operator and variables
// Ternary conditional operator 
font      = input_font == "Small"          ? size.small              : size.normal
pos0      = input_direction == "Bullish"   ? position.top_right      : position.bottom_right
pos1      = input_direction == "Bullish"   ? position.top_left       : position.bottom_left
col       = input_direction == "Bullish"   ? array.get(arr_color, 1) : array.get(arr_color, 2)
style_0   = input_direction == "Bullish"   ? label.style_label_up    : label.style_label_down
style_1   = input_direction == "Bullish"   ? label.style_label_down  : label.style_label_up
dir       = input_direction == "Bullish"   ? -1                      : 1
dir_title = input_direction == "Bullish"   ? "Bullish"               : "Bearish"
pattern   = input_pattern   == "Gartley"   ? "Gartley"               :
             input_pattern  == "Crab"      ? "Crab"                  :
             input_pattern  == "Deep Crab" ? "Deep Crab"             :
             input_pattern  == "Butterfly" ? "Butterfly"             :
             "Bat" 
type      = input_type      == "Perfect"   ? "Perfect"               : "Ideal"

// variables
cn = color.new(color.blue, 100)
cb = color.black
sc = label.style_label_center

// 3. Custom functions
// Label custom functions
fun_label_XABCD(_arr0) =>
    var label X  = na, label.delete(X),  X  := label.new(x = bar_index[50], y = dir * array.get(_arr0, 0), text = "X",                 color = cn,         textcolor = col, style = style_0, size = size.large)
    var label A  = na, label.delete(A),  A  := label.new(x = bar_index[40], y = dir * array.get(_arr0, 1), text = "A",                 color = cn,         textcolor = col, style = style_1, size = size.large)
    var label B  = na, label.delete(B),  B  := label.new(x = bar_index[30], y = dir * array.get(_arr0, 2), text = "B",                 color = cn,         textcolor = col, style = style_0, size = size.large)
    var label C  = na, label.delete(C),  C  := label.new(x = bar_index[20], y = dir * array.get(_arr0, 3), text = "C",                 color = cn,         textcolor = col, style = style_1, size = size.large)
    var label D  = na, label.delete(D),  D  := label.new(x = bar_index[10], y = dir * array.get(_arr0, 4), text = "D",                 color = cn,         textcolor = col, style = style_0, size = size.large)
    // var test = table.new(position = position.top_right, columns = 1, rows = 5, bgcolor = color.yellow, border_width = 1)
    // table.cell(test,     0, 0,    bgcolor = cb,           text_size = font,        text_color = color.orange, text = "X " + tostring(label.get_y(X), "0.000"))
    // table.cell(test,     0, 1,    bgcolor = cb,           text_size = font,        text_color = color.orange, text = "A " + tostring(label.get_y(A), "0.000"))
    // table.cell(test,     0, 2,    bgcolor = cb,           text_size = font,        text_color = color.orange, text = "B " + tostring(label.get_y(B), "0.000"))
    // table.cell(test,     0, 3,    bgcolor = cb,           text_size = font,        text_color = color.orange, text = "C " + tostring(label.get_y(C), "0.000"))
    // table.cell(test,     0, 4,    bgcolor = cb,           text_size = font,        text_color = color.orange, text = "D " + tostring(label.get_y(D), "0.000"))
fun_label_rng(_arr1, _arr2, _color) =>
    var label XB = na, label.delete(XB), XB := label.new(x = bar_index[40], y = dir * array.get(_arr1, 0), text = array.get(_arr2, 0), color = color.red,  textcolor = cb,  style = sc,      size = size.large)
    var label AC = na, label.delete(AC), AC := label.new(x = bar_index[30], y = dir * array.get(_arr1, 1), text = array.get(_arr2, 1), color = color.red,  textcolor = cb,  style = sc,      size = size.large)
    var label BD = na, label.delete(BD), BD := label.new(x = bar_index[20], y = dir * array.get(_arr1, 2), text = array.get(_arr2, 2), color = color.blue, textcolor = cb,  style = sc,      size = size.large)
    var label XD = na, label.delete(XD), XD := label.new(x = bar_index[30], y = dir * array.get(_arr1, 3), text = array.get(_arr2, 3), color = _color,     textcolor = cb,  style = sc,      size = size.large)
// Line custom functions
fun_line_solid(_arr0) =>
    var line XA = na, line.delete(XA)
    var line AB = na, line.delete(AB)
    var line BC = na, line.delete(BC)
    var line CD = na, line.delete(CD)
    XA := line.new(x1 = bar_index[50], y1 = dir * array.get(_arr0, 0), x2 = bar_index[40], y2 = dir * array.get(_arr0, 1), color = col, width = 4)
    AB := line.new(x1 = bar_index[40], y1 = dir * array.get(_arr0, 1), x2 = bar_index[30], y2 = dir * array.get(_arr0, 2), color = col, width = 4)
    BC := line.new(x1 = bar_index[30], y1 = dir * array.get(_arr0, 2), x2 = bar_index[20], y2 = dir * array.get(_arr0, 3), color = col, width = 4)
    CD := line.new(x1 = bar_index[20], y1 = dir * array.get(_arr0, 3), x2 = bar_index[10], y2 = dir * array.get(_arr0, 4), color = col, width = 4)
fun_line_dot(_arr0) =>
    var line XD = na, line.delete(XD)
    var line AC = na, line.delete(AC)
    var line XB = na, line.delete(XB)
    var line BD = na, line.delete(BD)
    XD := line.new(x1 = bar_index[50], y1 = dir * array.get(_arr0, 0), x2 = bar_index[10], y2 = dir * array.get(_arr0, 4), color = col, style = line.style_dotted)
    AC := line.new(x1 = bar_index[40], y1 = dir * array.get(_arr0, 1), x2 = bar_index[20], y2 = dir * array.get(_arr0, 3), color = col, style = line.style_dotted)
    XB := line.new(x1 = bar_index[50], y1 = dir * array.get(_arr0, 0), x2 = bar_index[30], y2 = dir * array.get(_arr0, 2), color = col, style = line.style_dotted)
    BD := line.new(x1 = bar_index[30], y1 = dir * array.get(_arr0, 2), x2 = bar_index[10], y2 = dir * array.get(_arr0, 4), color = col, style = line.style_dotted)

// 4. array and custom functions for table
table[] arr_table = array.new_table()
fun_table_top()   => table.new(position = position.top_center,    rows = 2, columns = 4, bgcolor = cn)
fun_table_right() => table.new(position = pos0,                   rows = 5, columns = 3, bgcolor = cn, border_color = cn, border_width = 1)
fun_table_left()  => table.new(position = pos1,                   rows = 3, columns = 4, bgcolor = cn, border_color = cn, border_width = 1)
fun_table_na()    => table.new(position = position.middle_center, rows = 1, columns = 1, bgcolor = cn)

array.push(arr_table, fun_table_top())   // 0 Table title
array.push(arr_table, fun_table_right()) // 1 Table range
array.push(arr_table, fun_table_left())  // 2 Table reference
array.push(arr_table, fun_table_na())    // 3 Table na

fun_table_title() =>
    table.cell(array.get(arr_table, 0),     0, 0,    bgcolor = cb,           text_size = size.normal, text_color = color.yellow, text = "Harmonic")
    table.cell(array.get(arr_table, 0),     1, 0,    bgcolor = cb,           text_size = size.normal, text_color = color.yellow, text = "Patterns")
    table.cell(array.get(arr_table, 0),     2, 0,    bgcolor = cb,           text_size = size.normal, text_color = color.yellow, text = "Volume 1")
    table.cell(array.get(arr_table, 0),     0, 1,    bgcolor = cb,           text_size = size.normal, text_color = col,          text = dir_title)
    table.cell(array.get(arr_table, 0),     1, 1,    bgcolor = cb,           text_size = size.normal, text_color = color.blue,   text = type)
    table.cell(array.get(arr_table, 0),     2, 1,    bgcolor = cb,           text_size = size.normal, text_color = color.blue,   text = pattern)
fun_table_rng(_arr2, _col) =>
    table.cell(array.get(arr_table, 1),     0, 0,    bgcolor = cn,           text_size = font,        text_color = color.orange, text = " ")
    table.cell(array.get(arr_table, 1),     0, 1,    bgcolor = color.yellow, text_size = font,        text_color = cb,           text = array.get(arr_title_table, 0))
    table.cell(array.get(arr_table, 1),     0, 2,    bgcolor = color.yellow, text_size = font,        text_color = cb,           text = array.get(arr_title_table, 1))
    table.cell(array.get(arr_table, 1),     0, 3,    bgcolor = color.yellow, text_size = font,        text_color = cb,           text = array.get(arr_title_table, 2))
    table.cell(array.get(arr_table, 1),     0, 4,    bgcolor = color.yellow, text_size = font,        text_color = cb,           text = array.get(arr_title_table, 3))
    table.cell(array.get(arr_table, 1),     1, 0,    bgcolor = cn,           text_size = font,        text_color = cb,           text = " ")
    table.cell(array.get(arr_table, 1),     1, 1,    bgcolor = color.red,    text_size = font,        text_color = cb,           text = array.get(_arr2, 0))
    table.cell(array.get(arr_table, 1),     1, 2,    bgcolor = color.red,    text_size = font,        text_color = cb,           text = array.get(_arr2, 1))
    table.cell(array.get(arr_table, 1),     1, 3,    bgcolor = color.blue,   text_size = font,        text_color = cb,           text = array.get(_arr2, 2))
    table.cell(array.get(arr_table, 1),     1, 4,    bgcolor = _col,         text_size = font,        text_color = cb,           text = array.get(_arr2, 3))
    for _row = 0 to 3
        table.cell(array.get(arr_table, 1), 2, _row, bgcolor = cn,           text_size = font,        text_color = color.orange, text = " ")
fun_table_ideal(_id) =>
    table.cell(array.get(arr_table, 2),     0, 0,    bgcolor = cn,           text_size = font,        text_color = color.white, text = "")
    table.cell(array.get(arr_table, 2),     0, 1,    bgcolor = color.black,  text_size = font,        text_color = color.white, text = "Reference")
    table.cell(array.get(arr_table, 2),     1, 1,    bgcolor = color.teal,   text_size = font,        text_color = color.black, text = "Bullish (M)")
    table.cell(array.get(arr_table, 2),     2, 1,    bgcolor = color.red,    text_size = font,        text_color = color.black, text = "Bearish (W)")
    table.cell(array.get(arr_table, 2),     0, 2,    bgcolor = color.black,  text_size = font,        text_color = color.white, text = "Volume 1")
    table.cell(array.get(arr_table, 2),     1, 2,    bgcolor = color.teal,   text_size = font,        text_color = color.black, text = "Page " + array.get(arr_ideal_bull, _id))
    table.cell(array.get(arr_table, 2),     2, 2,    bgcolor = color.red,    text_size = font,        text_color = color.black, text = "Page " + array.get(arr_ideal_bear, _id))
fun_table_perfect(_id) =>
    table.cell(array.get(arr_table, 2),     0, 0,    bgcolor = cn,           text_size = font,        text_color = color.white, text = "")
    table.cell(array.get(arr_table, 2),     0, 1,    bgcolor = color.black,  text_size = font,        text_color = color.white, text = "Reference")
    table.cell(array.get(arr_table, 2),     1, 1,    bgcolor = color.teal,   text_size = font,        text_color = color.black, text = "Bullish (M)")
    table.cell(array.get(arr_table, 2),     2, 1,    bgcolor = color.red,    text_size = font,        text_color = color.black, text = "Bearish (W)")
    table.cell(array.get(arr_table, 2),     0, 2,    bgcolor = color.black,  text_size = font,        text_color = color.white, text = "Volume 1")
    table.cell(array.get(arr_table, 2),     1, 2,    bgcolor = color.teal,   text_size = font,        text_color = color.black, text = "Page " + array.get(arr_perfect_bull, _id))
    table.cell(array.get(arr_table, 2),     2, 2,    bgcolor = color.red,    text_size = font,        text_color = color.black, text = "Page " + array.get(arr_perfect_bear, _id))
fun_table_not() =>
    table.cell(array.get(arr_table, 3),     0, 0,    bgcolor = cb,           text_size = font,        text_color = color.white, text = "Perfect Pattern\nnot available")
// 5. Compile custom functions
// ideal patterns custom functions
fun_ideal(_expr, _arr0, _arr1, _arr2, _col, _id) =>
    if input_type == "Ideal" and input_pattern == _expr
        fun_label_XABCD(_arr0)
        fun_label_rng(  _arr1, _arr2, _col)
        fun_line_solid( _arr0)
        fun_line_dot(   _arr0)
        fun_table_rng(  _arr2, _col)
        fun_table_ideal(  _id)
// perfect patterns custom functions
fun_perfect(_expr, _arr0, _arr1, _arr2, _col, _id) =>
    if input_type == "Perfect" and input_pattern == _expr
        fun_label_XABCD(_arr0)
        fun_label_rng(  _arr1, _arr2, _col)
        fun_line_solid( _arr0)
        fun_line_dot(   _arr0)
        fun_table_rng(  _arr2, _col)
        fun_table_perfect(  _id)

// 6. Construct
fun_table_title()
// construct ideal patterns
fun_ideal(  "Bat",       arr_float_i_Bat,       arr_dir_i_Bat,       arr_label_i_Bat,       color.red,  0)
fun_ideal(  "Gartley",   arr_float_i_Gartley,   arr_dir_i_Gartley,   arr_label_i_Gartley,   color.red,  1)
fun_ideal(  "Crab",      arr_float_i_Crab,      arr_dir_i_Crab,      arr_label_i_Crab,      color.blue, 2)
fun_ideal(  "Deep Crab", arr_float_i_DeepCrab,  arr_dir_i_DeepCrab,  arr_label_i_DeepCrab,  color.blue, 3)
fun_ideal(  "Butterfly", arr_float_i_Butterfly, arr_dir_i_Butterfly, arr_label_i_Butterfly, color.blue, 4)
// construct perfect patterns
fun_perfect("Bat",       arr_float_p_Bat,       arr_dir_p_Bat,       arr_label_p_Bat,       color.red,  0)
fun_perfect("Gartley",   arr_float_p_Gartley,   arr_dir_p_Gartley,   arr_label_p_Gartley,   color.red,  1)
fun_perfect("Crab",      arr_float_p_Crab,      arr_dir_p_Crab,      arr_label_p_Crab,      color.blue, 2)
fun_perfect("Butterfly", arr_float_p_Butterfly, arr_dir_p_Butterfly, arr_label_p_Butterfly, color.blue, 3)

// 7. if statement
if input_type == "Perfect" and input_pattern == "Deep Crab"
    fun_table_not()
